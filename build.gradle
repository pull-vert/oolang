plugins {
    id 'net.researchgate.release'
}

println("Using Gradle version: $gradle.gradleVersion")

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    sourceCompatibility = JavaVersion.VERSION_13
    targetCompatibility = JavaVersion.VERSION_13

    repositories {
        jcenter()
    }

    dependencies {
        // import BOM
        testImplementation platform("org.junit:junit-bom:$junit_bom_version")

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'failed', 'skipped'
            showStandardStreams = true
        }
    }

    jacoco {
        toolVersion = "0.8.5"
    }

    jacocoTestReport {
        dependsOn(test)

        reports {
            html.enabled = true
            xml.enabled = true
        }
    }

    publishing {
        repositories {
            maven {
                def user = 'pull-vert'
                def repo = 'oolang'
                def name = 'oolang'
                url = "https://api.bintray.com/maven/$user/$repo/$name/;publish=0"

                credentials {
                    username = bintray_user //this usually comes from gradle.properties file in ~/.gradle
                    password = bintray_api_key //this usually comes from gradle.properties file in ~/.gradle
                }
            }
        }

        publications {
            maven(MavenPublication) {
                artifactId = project.name
                from components.java
//                artifact sourcesJar todo check if still required
//                artifact javadocJar todo check if still required
                pom {
                    name = project.name
                    description = 'The Oolang JVM language'
                    url = 'https://github.com/pull-vert/oolang'
                    licenses {
                        license {
                            name = 'The Unlicence'
                            url = 'https://unlicense.org'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/pull-vert/oolang.git'
                        url = 'https://github.com/pull-vert/oolang.git'
                    }
                }
            }
        }
    }
}

// Workaround for https://github.com/researchgate/gradle-release/issues/144
task build {
    dependsOn subprojects.findResults { it.tasks.findByName('build') }
}

// execute ./gradlew wrapper then remove .gradle directory when version change
wrapper {
    gradleVersion = '6.0'
    distributionType = Wrapper.DistributionType.ALL
}
